
repositories {
	mavenCentral()
	mavenLocal()
	maven { url "https://dl.bintray.com/kotlin/kotlinx" }
	maven { url "https://dl.bintray.com/kotlin/ktor" }
	flatDir {
		dirs 'lib'
	}
}

apply plugin: 'distribution'

dependencies {
	compile project(':common')

	// Local files
	compile(name: 'worldwind')
	compile(name: 'java-engine')
	compile(name: 'jython-engine')

	// Old libs
	compile('org.eclipse.jdt.core.compiler:ecj:4.3.1')
	compile('org.python:jython:2.2.1')

	// MySQL
	runtime('mysql:mysql-connector-java:5.1.14')

	// Kotlin
	compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")
	compile("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")

	// Ktor
	compile("io.ktor:ktor-server-netty:$ktorVersion")

	// Logging
	compile "ch.qos.logback:logback-classic:1.2.1"

	// JUnit
	testCompile("junit:junit:4.12")
}

test {
	workingDir = new File('../dist')
}

task gameJar(type: Jar, dependsOn: classes) {
	from(sourceSets.main.output)
	archiveName = 'l2t-game-server.jar'
	exclude('**/loginserver/**')
	exclude('**/accountmanager/**')
	exclude('**/gsregistering/**')
	manifest {
		attributes(
				'Class-Path': configurations.runtime.collect({ './lib/' + it.getName() }).join(' '),
				'Main-Class': 'l2server.gameserver.GameApplicationKt')
	}
}

artifacts {
	archives gameJar
}

sourceSets {
	main {
		java {
			srcDirs '../dist/data/scripts'
		}
	}
}

distributions {
	main {
		contents {
			from('dist') {
				includeEmptyDirs(false)
				exclude('log/')
			}
			into('lib') {
				from(configurations.runtime)
				//from('/lib')
			}
			from(gameJar)
			exclude('.gitignore')
			exclude('log/')
		}
	}
}
