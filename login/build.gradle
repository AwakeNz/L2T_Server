
repositories {
	mavenCentral()
	mavenLocal()
	maven { url "https://dl.bintray.com/kotlin/kotlinx" }
	maven { url "https://dl.bintray.com/kotlin/ktor" }
}

apply plugin: 'distribution'

dependencies {
	compile project(':common')

	// MySQL
	runtime('mysql:mysql-connector-java:5.1.14')

	// Kotlin
	compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
	compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

	// Ktor
	compile("io.ktor:ktor-server-netty:$ktorVersion")

	// Logging
	compile "ch.qos.logback:logback-classic:1.2.1"

	// JUnit
	testCompile("junit:junit:4.12")
}

test {
	workingDir = new File('../dist')
}

task loginJar(type: Jar, dependsOn: classes) {
	from(sourceSets.main.output)
	archiveName = 'l2t-login-server.jar'
	manifest {
		attributes(
				'Class-Path': configurations.runtime.collect({ './lib/' + it.getName() }).join(' '),
				'Main-Class': 'l2server.loginserver.LoginApplicationKt')
	}
}

artifacts {
	archives loginJar
}

distributions {
	main {
		contents {
			from('dist') {
				includeEmptyDirs(false)
				exclude('log/')
			}
			into('lib') {
				from(configurations.runtime)
				//from('/lib')
			}
			from(loginJar)
			exclude('.gitignore')
			exclude('log/')
		}
	}
}
