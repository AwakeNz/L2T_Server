
repositories {
	mavenCentral()
	mavenLocal()
}

apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'distribution'

springBoot {
	mainClassName = "l2server.loginserver.LoginApplicationKt"
}

dependencies {
	compile project(':common')

	// MySQL
	runtime('mysql:mysql-connector-java:5.1.14')

	// Kotlin
	compile('com.fasterxml.jackson.module:jackson-module-kotlin')
	compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
	compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

	// Spring
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-security')

	// JUnit
	testCompile("junit:junit:4.12")

	// Spring test
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
}

test {
	workingDir = new File('../dist')
}

task loginJar(type: Jar, dependsOn: classes) {
	from(sourceSets.main.output)
	archiveName = 'l2t-login-server.jar'
	manifest {
		attributes(
				'Class-Path': configurations.runtime.collect({ './lib/' + it.getName() }).join(' '),
				'Main-Class': 'l2server.loginserver.LoginApplicationKt')
	}
}

artifacts {
	archives loginJar
}

distributions {
	main {
		contents {
			from('dist') {
				includeEmptyDirs(false)
				exclude('log/')
			}
			into('lib') {
				from(configurations.runtime)
				//from('/lib')
			}
			from(loginJar)
			exclude('.gitignore')
			exclude('log/')
		}
	}
}